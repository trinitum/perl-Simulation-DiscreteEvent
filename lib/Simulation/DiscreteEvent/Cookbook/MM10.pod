=pod

=head1 Modelling M/M/1/0 queue

M/M/1/0 queue is usually first example in any book on queueing theory. Let's
see how we can build the model of this system using
L<Simulation::DiscreteEvent>.

In M/M/1/0 system customers arrive in random moments of time independently of
each over. The interval between moments of arrival is a random variable
distributed exponentially, i.e. its density function is
I<< f(t)=lambda*exp(-lambda*t), t>0 >>. The I<lambda> parameter is arrival rate
-- the average number of customers arriving in a unit of time. There's a
single server in the system which serves customers. The time required to serve
a customer is exponentially distributed random variable. The density function is 
I<< f(t)=mu*exp(-mu*t), t>0 >>. The I<mu> parameter determines the average number
of customers that can be served in a unit of time. If customer arrives while
server is still busy with another customer, then new customer is turned away
and leaves the system. Let's try to determine probability that ariived customer
will be turned away.

In discrete-event simulation we assume that system may change its state only in
discrete moments of time then some event occurs. Between events the state of
the system is constant. Before simulation we're scheduling some events for
certain moments, and then during simulation handling these events. Inside event
handler we can schedule some additional events, so simulation may run
endlessly. We can limit number of events that will be generated, or specify
model time at which simulation should be stopped.

The base class for model is L<Simulation::DiscreteEvent>. Instance of this
class manages events' queue and invokes event handlers. Here's how we will use it:

    use Simulation::DiscreteEvent;
    my $model = Simulation::DiscreteEvent->new();

Now we should add server unit to the model. Server unit contains event
handlers. Each server is inherited from L<Simulation::DiscreteEvent::Server>
class. This class contains dispatcher that invokes different handlers depending
on event, so our server class should only define event handlers and specify for
each handler on which event it should be invoked. Here's the server class:

    package Simulation::DiscreteEvent::CB::MM10;
    use Moose;
    use Math::Random qw(random_exponential);
    BEGIN { extends 'Simulation::DiscreteEvent::Server' }

    # server state
    has busy => ( is => 'rw', default => 0 );

    # arrival rate
    has lambda => ( is => 'rw', required => 1 );

    # serving rate
    has mu => ( is => 'rw', required => 1 );

    # number of served customers
    has served => (
        is      => 'rw',
        traits  => ['Counter'],
        default => 0,
        handles => { inc_served => 'inc' }
    );

    # number of rejected customers
    has rejected => (
        is      => 'rw',
        traits  => ['Counter'],
        default => 0,
        handles => { inc_rejected => 'inc' }
    );

    # New customerr arrived
    sub arrival : Event(arrival) {
        my $self = shift;
        my $next_time = $self->model->time 
            + random_exponential( 1, $self->lambda );
        $self->model->schedule( $next_time, $self, 'arrival' );
        if ( $self->busy ) {
            $self->inc_rejected;
        }
        else {
            $srv_time = $self->model->time
                + random_exponential( 1, $self->mu );
            $self->model->schedule( $srv_time, $self, 'finished' );
            $self->busy(1);
        }
    }

    # Customer served
    sub finish : Event(finished) {
        my $self = shift;
        $self->inc_served;
        $self->busy(0);
    }

    no Moose;
    __PACKAGE__->meta->make_immutable;

As you can see we're using L<Moose>, if you don't know what Moose is you should
read L<Moose::Manual>. We're defining several attributes and two event
handlers. Each event handler is marked with C<Event> method attribute, e.g.
finish method has C<Event(finished)> attribute on it, that shows that event
'finished' should be handled by finish method.

Here are two types of events: 'arrival' -- that means that new customer has
arrived, and 'finished' that means that server has finished to serve customer.
Let's start from 'arrival' handler. First we're computing time when next
customer will arrive, for that we're adding to current model time
(C<< $self->model->time >>) random value with exponential distribution. After
that we're scheduling event for the next customer.
L<< Schedule|Simulation::DiscreteEvent/"$self->schedule" >> function accepts three
arguments (fourth argument is optional): time of the event to schedule,
reference to the server that should handle event, and type of the event. After
scheduling event for the next customer we're returning to current and checking
if server busy or not. If server already busy, then we rejecting arrived
customer. If server is free, customer is started to be served. We're computing
time when serving will be finished and scheduling that event. Also we're
changing server state to busy.

Handler for 'finished' event is much simpler. We're just increasing number of
served customers and chenging server state back to free.


=head1 AUTHOR

Pavel Shaydo, C<< <zwon at cpan.org> >>

=head1 BUGS

Please report any bugs or feature requests to C<bug-simulation-discreteevent at rt.cpan.org>, or through
the web interface at L<http://rt.cpan.org/NoAuth/ReportBug.html?Queue=Simulation-DiscreteEvent>.  I will be notified, and then you'll
automatically be notified of progress on your bug as I make changes.

=head1 SEE ALSO

L<Simulation::DiscreteEvent>

=head1 LICENSE AND COPYRIGHT

Copyright 2010 Pavel Shaydo.

This program is free software; you can redistribute it and/or modify it
under the terms of either: the GNU General Public License as published
by the Free Software Foundation; or the Artistic License.

See http://dev.perl.org/licenses/ for more information.


=cut

